apply plugin: 'info.solidsoft.pitest'

sourceSets {
    testUnit {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java {
            srcDir file('src/unit-test/java')
        }
        resources.srcDir file('src/unit-test/resources')
    }
}

dependencies {
    implementation project(':dao-api')
    implementation project(':domain')
    implementation project(':utils')

    testImplementation project(path: ':utils', configuration: 'testOutput')
}

configurations {
    testOutput
    testOutput.extendsFrom(testUnitImplementation)
}

task testJar(type: Jar, dependsOn: testClasses) {
    archiveClassifier = 'test'
    from sourceSets.testUnit.output
}

artifacts {
    archives testJar
}

publishing {
    publications {
        AzureArtValidators(MavenPublication) {
            groupId project.group
            artifactId 'validators'
            artifact testJar
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            name = "ValidatorsAzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}

tasks.register('unit', Test) {
    description = "Runs unit tests"
    group = "Verification"
    testClassesDirs = sourceSets.testUnit.output.classesDirs
    classpath = sourceSets.testUnit.runtimeClasspath
    failFast = true
}

check.dependsOn unit