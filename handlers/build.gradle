apply plugin: 'info.solidsoft.pitest'

dependencies {
    implementation project(':domain')
    implementation project(':handlers-api')
    implementation project(':producers-api')
    implementation project(':utils')
    implementation project(':services-api')
    implementation project(':transformers')
    implementation project(':validators')
}

sourceSets {
    testUnit {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java {
            srcDir file('src/unit-test/java')
        }
        resources.srcDir file('src/unit-test/resources')
    }
}

configurations {
    testOutput
    testOutput.extendsFrom(testUnitImplementation)
}

task testJar(type: Jar, dependsOn: testClasses) {
    archiveClassifier = 'test'
    from sourceSets.testUnit.output
}

artifacts {
    archives testJar
}

publishing {
    publications {
        AzureArtHandlers(MavenPublication) {
            groupId project.group
            artifactId 'handlers'
            artifact testJar
            version project.version
            from components.java
        }
    }

    repositories {
        maven {
            name = "HandlersAzureArtifacts"
            url = uri("https://pkgs.dev.azure.com/hmcts/Artifacts/_packaging/hmcts-lib/maven/v1")
            credentials {
                username = System.getenv("AZURE_DEVOPS_ARTIFACT_USERNAME")
                password = System.getenv("AZURE_DEVOPS_ARTIFACT_TOKEN")
            }
        }
    }
}

tasks.register('unit', Test) {
    description = "Runs unit tests"
    group = "Verification"
    testClassesDirs = sourceSets.testUnit.output.classesDirs
    classpath = sourceSets.testUnit.runtimeClasspath
    failFast = true
}

check.dependsOn unit