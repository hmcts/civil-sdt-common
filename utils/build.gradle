apply plugin: 'info.solidsoft.pitest'

sourceSets {
    testUnit {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java {
            srcDir file('src/unit-test/java')
        }
        resources.srcDir file('src/unit-test/resources')
    }

    testIntegration {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
        java {
            srcDir file('src/integ-test/java')
        }
        resources.srcDir file('src/integ-test/resources')
    }
}

configurations {
    testOutput
    testOutput.extendsFrom(testUnitImplementation)
}

dependencies {
    testOutput sourceSets.testUnit.output
}

task integration(type: Test) {
    description = "Runs integration tests"
    group = "Verification"
    testClassesDirs = sourceSets.testIntegration.output.classesDirs
    classpath = sourceSets.testIntegration.runtimeClasspath
    failFast = true
}

repositories {
    mavenCentral()
}

task testJar(type: Jar, dependsOn: testClasses) {
    classifier = 'test'
    from sourceSets.testUnit.output
}

task testIntegJar(type: Jar, dependsOn: testClasses) {
    classifier = 'integ-test'
    from sourceSets.testIntegration.output
}

artifacts {
    archives testJar
    archives testIntegJar
}

publishing {
    publications {
        "$project.name"(MavenPublication) {
            groupId project.group
            artifactId project.name
            artifact testJar
            artifact testIntegJar
            version project.version
            from components.java
        }
    }
}

tasks.register('unit', Test) {
    description = "Runs unit tests"
    group = "Verification"
    testClassesDirs = sourceSets.testUnit.output.classesDirs
    classpath = sourceSets.testUnit.runtimeClasspath
    failFast = true
}

unit.mustRunAfter integration
check.dependsOn unit